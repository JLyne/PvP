import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
    id 'maven-publish'
}

repositories {
    mavenCentral()
    mavenLocal()

    maven {
        url "https://papermc.io/repo/repository/maven-public/"
    }

    maven {
        url 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

dependencies {
    compileOnly 'org.jetbrains:annotations:24.0.1'
    compileOnly "io.papermc.paper:paper-api:1.19.4-R0.1-SNAPSHOT"
    compileOnly 'me.clip:placeholderapi:2.11.3'

    implementation 'cloud.commandframework:cloud-paper:1.8.3'
    implementation 'cloud.commandframework:cloud-minecraft-extras:1.8.3'
    implementation 'cloud.commandframework:cloud-annotations:1.8.3'
}

group 'uk.co.notnull'
version '1.1-SNAPSHOT'

java.sourceCompatibility = JavaVersion.VERSION_17
java.targetCompatibility = JavaVersion.VERSION_17

tasks {
    processResources {
        from("${project.rootDir}/src/main/resources") {
            duplicatesStrategy = DuplicatesStrategy.INCLUDE
            project.properties.findAll().each {
                prop ->
                    if (prop.value != null && prop.value instanceof java.lang.String) {
                        filter(ReplaceTokens, tokens: [(prop.key): prop.value])
                        filter(ReplaceTokens, tokens: [('project.' + prop.key): prop.value])
                    }
            }
        }
    }

    compileJava {
        options.compilerArgs += ["-Xlint:all", "-Xlint:-processing"]
        options.encoding = 'UTF-8'
    }

    shadowJar {
        archiveClassifier.set('')
    }

    build {
        dependsOn(shadowJar)
    }
}
